buildscript {
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/comfreshworks-1127")
        }
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/comfreshworks-1126")
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.freshdesk'

def static getEscapedString(value) {
    return "\"$value\"";
}

def getScmRevision = {
    String rev = 'git rev-parse --short HEAD'.execute().text.trim()
    return getEscapedString(rev)
}

def getScmBranch = {
    String branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
    return getEscapedString(branch)
}

android {
    compileSdkVersion 35
    buildToolsVersion "29.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion 24
        versionCode 642
        versionName "6.4.2"
        targetSdkVersion 35
        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildConfigField "long", 'BUILT_AT', System.currentTimeMillis() + 'L'
        buildConfigField "String", "SCM_REV", getScmRevision()
        buildConfigField "String", "SCM_BRANCH", getScmBranch()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'sdk-proguard-rules.pro'
        }
    }
}

ext {
    gsonVersion             = '2.8.0'
    picassoVersion          = '2.8'
    circleImageViewVersion  = '3.1.0'
    appcompatVersion        = '1.6.0'
    recyclerviewVersion     = '1.3.2'
    materialVersion         = '1.0.0'
    cardviewVersion         = '1.0.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "de.hdodenhof:circleimageview:$circleImageViewVersion"
    implementation 'com.freshworks.sdk:logging:0.0.23-alpha'
}
